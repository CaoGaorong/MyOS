## 实现打印函数

### 一、基本介绍

功能：

- put_str打印字符串
- put_char打印字符
- put_int打印整型数据

### 二、重点讲解

#### 1. 操作显卡介绍

在屏幕上打印，需要显示如下：

- 显示字符
- 显示光标

##### 1.1 显示字符

显示字符，直接**向显存里面写入字符和属性**即可。

- 显存的地址范围：

  - 0xb8000 - 0xbffff (0x7ffff个字节)

- 一个屏幕可以可以显示的字符

  - 一个屏幕可以显示80 * 25  = 2000个字符
  - 每个字符占用两个字节，也就是一个屏幕用到4000个字节

- 显示一个字符

  - 显示一个字符占用2个字节
  - 第1字节，是字符的ASCII码
  - 高1字节，是字符的属性（颜色之类的）

  > <img align="left" src="https://i.loli.net/2021/05/27/Fwbz86tGc4ZqS5o.png" alt="image-20210527103738256" style="zoom: 67%;" />



##### 1.2 操作光标

操作光标，需要**操作显卡控制器**。

如下图是部分显卡端口：

![image-20210527104147172](https://i.loli.net/2021/05/27/kyO1VtZGSAE2eIR.png)

- 注意，上图中的寄存器，只能算是，寄存器的”目录“
  - 要完成具体的操作，还需要给出**子寄存器号**
  - 就是像是系统调用中断，所有系统调用共用一个中断号，要执行具体的系统调用，就还要给出子中断号
  - 这样做是因为 端口号不够用
- 红框标出的是CRT Controller Register，是待会儿要用来操作光标的寄存器组
  - CRT Controller Register在端口那里，显示的是3x4h和3x5h，x的值，默认是5
- 上述寄存器分组，都分成了Address Register和Data Register
  - **Address Register**写入 **子寄存器号**
  - **Data Register** 取出/存入  **子寄存器中的数值**
  - 就像是内存中的MAR和MDR，往MAR写入内存地址，通过MDR取出/写入对应地址数据



对于CRT Controller Register，中的**子寄存器**可以是如下：

![image-20210527105522747](https://i.loli.net/2021/05/27/yvHjDb45WKPA8F3.png)

- 上表6-5中的寄存器都共用端口号0x3x5（也就是CRT Controller Register中的Data Register）
- 如何访问不同的寄存器？
  - 往CRT Controller Register中的Address Register（也就是端口0x3x4）写入子寄存器索引
  - 然后往CRT Controller Register 中的Data Register读取/写入数据



关于CRT Controller寄存器和子寄存器的关系如下图所示：

![](https://i.loli.net/2021/05/27/JoGuVvhxETeOKCX.png)

- 往Address Register（0x3x4端口号）可以访问具体的子寄存器
- Data Register表示一组寄存器，当Address寄存器的值不同，表示的寄存器也不同
  - 比如当Address寄存器是0x00，那么Data Register就是0x00号寄存器

#### 2. 打印字符实现思路

待打印的字符，只支持以下几种：

- 普通字符
- 转义字符
  - 退格
  - 回车换行



##### 2.1 操作光标

在打印字符前，要先获取光标的位置，因为字符是在光标所在的地方打印的。

打印完毕后，还需要设置光标的位置，这样好像光标的位置随着字符的打印而移动。



获取和设置光标的位置，需要操作显卡中的端口。

- 光标位置的高8位在CRT Controller Register的0E号寄存器
- 光标位置的第8位在CRT Controller Register的0F号寄存器

###### 2.1.1 获取光标的位置

- 获取光标位置的高8位：

  - 访问CRT Controller Register的Address Register，也就是0x03d4端口，设置子寄存器(0E号)的索引

    - ```assembly
      mov dx, 0x03d4  ;索引寄存器
      mov al, 0x0e	   ;用于提供光标位置的高8位
      out dx, al
      ```

  - 读取CRT Controller Register的Data Register，也就是0x03d5端口，取出该寄存器的值

    - ```assembly
      mov dx, 0x03d5  ;通过读写数据端口0x3d5来获得或设置光标位置 
      in al, dx	   ;得到了光标位置的高8位
      ```

- 获取光标位置的低8位：

  - 访问CRT Controller Register的Address Register，也就是0x03d4端口，设置子寄存器(0F号)的索引

    - ```assembly
      mov dx, 0x03d4  ;索引寄存器
      mov al, 0x0f	   ;用于提供光标位置的高8位
      out dx, al
      ```

  - 读取CRT Controller Register的Data Register，也就是0x03d5端口，取出该寄存器的值

    - ```assembly
      mov dx, 0x03d5  ;通过读写数据端口0x3d5来获得或设置光标位置 
      in al, dx	   ;得到了光标位置的高8位
      ```

###### 2.1.2 设置光标的位置

类似的，需要设置高8位和低8位。

只不过，需要把光标位置写入CRT Controller Register中的Data 寄存器

步骤也和读取光标位置类似：

- 写入光标位置的高8位
  - 选择要访问的子寄存器
    - 把子寄存器号给0x03d4端口
  - 把光标位置高8位写入
    - 光标位置高8位写入0x03d5端口
- 写入光标位置的低8位
  - 选择要访问的子寄存器
    - 把子寄存器号给0x03d4端口
  - 把光标位置低8位写入
    - 光标位置高8位写入0x03d5端口

##### 2.2 打印普通字符

打印普通字符，直接往显存中光标所在的位置里面写入字符即可。

```assembly
.put_other:
   shl bx, 1				  ; bx是光标位置，光标位置是用2字节表示,将光标值乘2,表示对应显存中的偏移字节
   mov [gs:bx], cl			  ; ascii字符本身
   inc bx
   mov byte [gs:bx],0x07		  ; 字符属性
   shr bx, 1				  ; 恢复老的光标值
   inc bx				  ; 下一个光标值
   cmp bx, 2000		   
   jl .set_cursor			  ; 若光标值小于2000,表示未写到显存的最后,则去设置新的光标值
```

- 打印之前要先获取光标位置（存储在bx中）
- 往光标位置写入字符和字符属性
- 如果当前的字符数没有超过一个屏幕（2000个）：就设置光标位置```.set_cursor```
- 如果超过了一个屏幕，就换行，并且滚动屏幕

##### 2.3 实现退格

```assembly
 .is_backspace:		      
;;;;;;;;;;;;       backspace的一点说明	     ;;;;;;;;;;
; 当为backspace时,本质上只要将光标移向前一个显存位置即可.后面再输入的字符自然会覆盖此处的字符
; 但有可能在键入backspace后并不再键入新的字符,这时在光标已经向前移动到待删除的字符位置,但字符还在原处,
; 这就显得好怪异,所以此处添加了空格或空字符0
   dec bx
   shl bx,1
   mov byte [gs:bx], 0x20		  ;将待删除的字节补为0或空格皆可
   inc bx
   mov byte [gs:bx], 0x07		  ; 设置字符属性
   shr bx,1
   jmp .set_cursor
```

- 把获取当前光标位置（放在bx中）
- 然后把光标位置的前一个位置，该位置的字符改为空格（包括字符属性）
- 然后设置光标的位置（相比较之前的，往前移动了一个）

##### 2.4 打印回车、换行符

- 对于回车符(CR)，是把光标移动到一行的开头
- 对于换行符(LF)，是把光标移动到另一行。
- 在Windows中，使用回车换行符(\r\n)表示换行；在Linux中，使用换行符(\n)表示换行；在Mac中，使用回车符(\r)表示换行。

```assembly
 .is_line_feed:				  ; 是换行符LF(\n)
 .is_carriage_return:			  ; 是回车符CR(\r)
					  ; 如果是CR(\r),只要把光标移到行首就行了。
   xor dx, dx				  ; dx是被除数的高16位,清0.
   mov ax, bx				  ; ax是被除数的低16位.
   mov si, 80				  ; 由于是效仿linux，linux中\n便表示下一行的行首，所以本系统中，
   div si				  ; 把\n和\r都处理为linux中\n的意思，也就是下一行的行首。
   sub bx, dx				  ; 光标值减去除80的余数便是取整
					  ; 以上4行处理\r的代码

 .is_carriage_return_end:                 ; 回车符CR处理结束
   add bx, 80
   cmp bx, 2000
 .is_line_feed_end:			  ; 若是LF(\n),将光标移+80便可。  
   jl .set_cursor
```

- 在这里，处理换行符 会包括 回车符
- 得到当前光标位置（放在bx中）
- 算出下一行开头的光标位置
  - 把当前光标位置，对80(一行有80个空位)取整（去除余数），得到本行开头的位置坐标
  - 然后再加上80（一行的空位），得到下一行的开头坐标
- 然后设置光标
- **注意：这里有可能会超过一个屏幕的范围，需要滚屏，滚屏操作在下面**

##### 2.6 滚屏

```assembly
;屏幕行范围是0~24,滚屏的原理是将屏幕的1~24行搬运到0~23行,再将第24行用空格填充
 .roll_screen:				  ; 若超出屏幕大小，开始滚屏
   cld  
   mov ecx, 960				  ; 一共有2000-80=1920个字符要搬运,共1920*2=3840字节.一次搬4字节,共3840/4=960次 
   mov esi, 0xb80a0			  ; 第1行行首
   mov edi, 0xb8000			  ; 第0行行首
   rep movsd				  

;;;;;;;将最后一行填充为空白
   mov ebx, 3840			  ; 最后一行首字符的第一个字节偏移= 1920 * 2
   mov ecx, 80				  ;一行是80字符(160字节),每次清理1字符(2字节),一行需要移动80次
 .cls:
   mov word [gs:ebx], 0x0720		  ;0x0720是黑底白字的空格键
   add ebx, 2
   loop .cls 
   mov bx,1920				  ;将光标值重置为1920,最后一行的首字符.
```

- 滚屏的原理是将屏幕的1~24行搬运到0~23行,再将第24行用空格填充。感觉好繁琐，，



#### 3. put_char函数实现

put_char函数就是打印单个字符，也就是上面将的打印字符实现思路。

先要获取光标位置，然后对不同的字符进行分析，普通字符就直接往光标所在的位置进行打印，如果是退格或者回车，就要实现对应的行为，然后设置光标的位置。

#### 4. put_str函数实现

put_str()函数是打印字符串，就是循环调用put_char函数，直到遇到'\0'

```assembly
put_str:
;由于本函数中只用到了ebx和ecx,只备份这两个寄存器
   push ebx
   push ecx
   xor ecx, ecx		      ; 准备用ecx存储参数,清空
   mov ebx, [esp + 12]	      ; 从栈中得到待打印的字符串地址 
.goon:
   mov cl, [ebx]
   cmp cl, 0		      ; 如果处理到了字符串尾,跳到结束处返回
   jz .str_over
   push ecx		      ; 为put_char函数传递参数
   call put_char
   add esp, 4		      ; 回收参数所占的栈空间
   inc ebx		      ; 使ebx指向下一个字符
   jmp .goon
.str_over:
   pop ecx
   pop ebx
   ret
```



#### 5. put_int函数实现

put_int是打印整型数字，起始就是要转成文本字符，然后输出。

对于put_int有以下限制：

- 仅仅支持正整数
- 输出是16进制
- put_int(x)函数使用C中的输出就是printf("%u",x);

一个整型数字大小是4个字节，32位，转成16进制，表示的范围是[0x0, 0xffff ffff)。

也就是最大值使用16进制表示，有8个十六进制字符，使用文本字符表示，就有8个ASCII码字符，需要8位。



### 三、代码演示

